START_MARKER

define(setup_request_test,[[
  test_do( add_constant("R", Protocols.HTTP.Server.Request()) )
  test_do([[
  class FD {
    inherit Stdio.FakeFile;
    void add(string s) {
      read_cb(0, s);
    }
  };
  add_constant("FD",FD(""));
  ]])
  test_do( R->attach_fd(FD, 0, lambda(object x){} ); )
]])

define(clear_request_test,[[
  test_do( add_constant("R") )
  test_do( add_constant("FD") )
]])

setup_request_test()

test_do( FD->add("GET /foo/bar+%20/zuul.html?ha=bla&esp=an&ha=xxor ") )
test_do( FD->add("HTTP/1.01\r") )
test_do( FD->add("\n") )
test_do( FD->add("Content-Length: 5\r\n") )
test_do( FD->add("Content-Length: 5\r\n") )
test_do( FD->add("Cookie:    A=a ; B=b;C=x\r\n") )
test_do( FD->add("Cookie: D=d;C=c\r\n") )
test_do( FD->add("User-Agent: testsuite.in\r\n") )
test_do( FD->add("\r\nH") )

// parse_request and parse_variables called.

test_eq( R->request_raw, "GET /foo/bar+%20/zuul.html?ha=bla&esp=an&ha=xxor HTTP/1.01") )
test_eq( R->request_type, "GET" )
test_eq( R->protocol, "HTTP/1.1" )
test_eq( R->full_query, "/foo/bar+%20/zuul.html?ha=bla&esp=an&ha=xxor" )
test_eq( R->query, "ha=bla&esp=an&ha=xxor" )
test_eq( R->not_query, "/foo/bar+%20/zuul.html" )

test_equal( R->variables, ([ "esp":"an", "ha":({"bla","xxor"}) ]) )
test_eq( R->request_headers["content-length"], "5" )
test_eq( sizeof(R->request_headers), 3 );

test_do( FD->add("ELL") )
test_do( FD->add("OMG") )

// finalized called.

test_eq( R->body_raw, "HELLO" )
test_equal( R->cookies, ([ "A":"a", "B":"b", "C":"c", "D":"d" ]) )
test_equal( R->misc, ([]) )

clear_request_test()

// FIXME: Test transfer encoding chunked
// FIXME: Test multipart/formdata

setup_request_test()

test_do( add_constant("req", "POST / HTTP/1.1\r\nUser-Agent: Mozilla/4.0 (compatible; MSIE 6.0; X11; Linux i686; en) Opera 9.00\r\nHost: localhost:8080\r\nAccept: text/html, application/xml;q=0.9, application/xhtml+xml, image/png, image/jpeg, image/gif, image/x-xbitmap, */*;q=0.1\r\nAccept-Language: en\r\nAccept-Encoding: deflate, gzip, x-gzip, identity, *;q=0\r\nReferer: http://localhost:8086/\r\nCookie: show_wurfl_state=65; I=78d69acdb6caee5d6b720b8ae37a387b3db9e10a46feee12a6e1717989f9b3c1; device_state=64\r\nCookie2: $Version=1\r\nContent-Length: 7\r\nContent-Type: application/x-www-form-urlencoded\r\n\r\na=b&c=d") )
test_do( FD->add(req) )

test_equal( R->variables, ([ "a":"b", "c":"d" ]) )
test_eq( R->raw, req )

test_do( add_constant("req") )
clear_request_test()

cond([[ all_constants()->thread_create && Stdio["__HAVE_CONCURRENT_CLOSE__"] ]], [[
  test_any([[
    class R {
      inherit Protocols.HTTP.Server.Request;
      void response_and_finish(mapping m, function|void log_cb)
      {
        if (!undefinedp(m->size)) {
          /* Lie about the content-length. */
          m->size += 5;
        }
        ::response_and_finish(m, log_cb);
      }
    };

    string request_thread(Stdio.File f)
    {
      Protocols.HTTP.Query q = Protocols.HTTP.Query();
      // Set some minimal timeouts.
      q->timeout = 1;
      q->data_timeout = 1;
      // Use the pipe for the request.
      q->con = f;
      // NB: The following are needed in order to simulate a keep-alive
      //     connection reuse request (and not connect to the real example.com).
      q->host = "example.com";
      q->port = 80;
      q->headers = ([ "connection": "keep-alive" ]);
      q->buf = "";
      return Protocols.HTTP.get_url("http://example.com/index.txt",
                                    UNDEFINED, UNDEFINED, q)->data();
    };

    mapping got_request(R r)
    {
      r->response_and_finish(([ "data": "Data underflow.",
                                "error": 200,
                                "size": 15,
                                "type": "text/plain",
                                "server": "Broken content-length",
                             ]));
    };

    Stdio.File f = Stdio.File();
    Stdio.File p = f->pipe(Stdio.PROP_BIDIRECTIONAL);

    R r = R();
    r->attach_fd(p, UNDEFINED, got_request);

    Thread.Thread t = Thread.Thread(request_thread, f);
    for (int i = 0; i < 20; i++) {
      if (t->status()) break;
      Pike.DefaultBackend(1.0);
    }
    if (!t->status()) {
      t->interrupt("Hang.\n");
      return "Timeout handler not called.";
    }
    if (f->is_open()) {
      return "File not closed by timeout handler.";
    }
    return t->wait();
  ]], "Data underflow.")
]])

END_MARKER
