#
# $Id: configure.in,v 1.29 2005/01/13 12:57:14 grubba Exp $
#
# Configure script for the odbc-module
#
# Henrik Grubbström
#

AC_INIT(odbc.c)
AC_CONFIG_HEADER(config.h)

AC_MODULE_INIT()

AC_PROG_CPP

OLD_LIBS=$LIBS
OLD_LDFLAGS=$LDFLAGS
OLD_CPPFLAGS=$CPPFLAGS

AC_ARG_WITH(odbc,  [  --without-odbc       no support for ODBC databases],[],[with_odbc=yes])
AC_ARG_WITH(odbc_implicit_commit,
		   [  --with-odbc-implicit-commit    implicitly commit statements (discouraged)])

if test "x$with_odbc_implicit_commit" = xyes; then
  AC_MSG_WARN([Enabling implicit commit])
  AC_DEFINE(ENABLE_IMPLICIT_COMMIT)
fi

PIKE_FEATURE_WITHOUT(Odbc)

if test x$with_odbc = xyes; then
  PIKE_FEATURE_NODEP(Odbc)

  if test "x$pike_cv_sys_os" = "xWindows_NT" ; then
    AC_CHECK_HEADERS(windows.h sql.h sqlext.h)

    if test "$ac_cv_header_sql_h$ac_cv_header_sqlext_h" = "yesyes"; then
      AC_DEFINE(HAVE_ODBC)
      LIBS="-lodbc32 $LIBS"
      pike_cv_odbc=yes

      cat <<EOF

ODBC support enabled.

EOF
    fi
  else
  
    pike_cv_odbc=no;
  
    AC_MSG_CHECKING(for the ODBC include-directory)
  
    AC_CACHE_VAL(pike_cv_odbc_include_dir, [
      if test "x$INFORMIXDIR" = "x" ; then 
  	foodir=''
      else
  	foodir=$INFORMIXDIR/cli/include
      fi
  
      pike_cv_odbc_include_dir=''
  
      # MIMER doesn't include the odbc header files.
  
      for bardir in $foodir $with_root/opt/*odbc/*/include \
  	  $with_root/usr/opt/*odbc/*/include $with_root/opt/*odbc/include \
	  $with_root/usr/opt/*odbc/include $with_root/usr/local/include \
	  $with_root/usr/local/odbc/include $with_root/usr/local/include/odbc \
  	  $with_root/usr/local/mysql/include \
	  $with_root/usr/local/mysql/include/mysql \
  	  $with_root/usr/odbc/include $with_root/usr/include/odbc \
          $with_root/usr/include $with_root/include/odbc $with_root/include; do
  	if test -d $bardir/.; then
  	  if ls $bardir/qeodbc.h >/dev/null 2>&1; then
  	    pike_cv_odbc_include_dir="$bardir $pike_cv_odbc_include_dir"
  	  else
  	    if ls $bardir/isql.h >/dev/null 2>&1; then
  	      pike_cv_odbc_include_dir="$bardir $pike_cv_odbc_include_dir"
  	    else
  	      :
  	    fi
  	  fi
  	else
  	  :
  	fi
      done
    ])
  
    AC_MSG_RESULT($pike_cv_odbc_include_dir)
  
    if test "x$pike_cv_odbc_include_dir" = x; then :; else
      for bardir in $pike_cv_odbc_include_dir; do
  	echo Adding $bardir to the include search path.
  	CPPFLAGS="-I$bardir ${CPPFLAGS}"
      done
    fi
  
    AC_MSG_CHECKING(for ODBC library-directory)
  
    AC_CACHE_VAL(pike_cv_odbc_lib_dir, [
  
      pike_cv_odbc_lib_dir=''
  
      # ${INFORMIXDIR:+$INFORMIXDIR/cli/dlls} does not work with stupid sh
      # /Hubbe
  
      if test "x$INFORMIXDIR" = "x" ; then 
  	foodir=''
      else
  	foodir=$INFORMIXDIR/cli/dlls
      fi
  
      for bardir in $foodir $with_root/opt/*odbc/*/lib \
	  $with_root/usr/opt/*odbc/*/lib $with_root/opt/*odbc/lib \
	  $with_root/usr/opt/*odbc/lib $with_root/usr/local/lib \
          $with_root/usr/local/odbc/lib $with_root/usr/local/lib/odbc \
	  $with_root/usr/odbc/lib $with_root/usr/lib/odbc $with_root/usr/lib \
	  $with_root/lib/odbc $with_root/lib; do
  	if test -d $bardir/.; then
  	  if ls $bardir/*odbc* >/dev/null 2>&1 ; then
  	    pike_cv_odbc_lib_dir="$bardir $pike_cv_odbc_lib_dir"
  	  else
  	    :
  	  fi
  	else
  	  :
  	fi
      done
    ])
  
    AC_MSG_RESULT($pike_cv_odbc_lib_dir)
  
    if test "x$pike_cv_odbc_lib_dir" = x; then :; else
      for bardir in $pike_cv_odbc_lib_dir; do
  	echo Adding $bardir to the library search path.
  	LDFLAGS="-L$bardir -R$bardir ${LDFLAGS}"
      done
    fi
  
    AC_CHECK_HEADERS(isql.h isqlext.h)
  
    pike_cv_odbc=no;
  
    if test "$ac_cv_header_isql_h$ac_cv_header_isqlext_h" = "yesyes"; then
  
      AC_CHECK_LIB(iodbcinst, SQLGetConfigMode)
  
      AC_CHECK_LIB(iodbc, SQLConnect, [
  	LIBS="-liodbc $LIBS"
  	pike_cv_odbc=yes;
      ], [])
  
    else
      :
    fi

    if test "x$pike_cv_odbc" = xyes; then
      :
    else
  
      # Header file
  
      AC_CHECK_HEADERS(windows.h qeodbc.h sql.h sqlext.h)
  
      if test "$ac_cv_header_sql_h$ac_cv_header_sqlext_h" = "yesyes"; then
  
  	# System libs which might be needed
  
  	if echo $LIBS|grep -- -lsocket >&5 2>&5; then
  	  :
  	else
  	  AC_CHECK_LIB(socket, socket, [
  	    LIBS="-lsocket $LIBS"
  	  ], [])
  	fi
  	if echo $LIBS|grep -- -lnsl >&5 2>&5; then
  	  :
  	else
  	  AC_CHECK_LIB(nsl, gethostbyname, [
  	    LIBS="-lnsl $LIBS"
  	  ], [])
  	fi
  	if echo $LIBS|grep -- -lm >&5 2>&5; then
  	  :
  	else
  	  AC_CHECK_LIB(m, floor, [
  	    LIBS="-lm $LIBS"
  	  ], [])
  	fi
  
  	AC_CHECK_LIB(odbc, SQLConnect, [
  	  LIBS="-lodbc $LIBS"
  	  pike_cv_odbc=yes;
  	], [])
      else
  	:
      fi
  
    fi

    if test x$pike_cv_odbc = xno; then
      # Restore variables, so we don't link with unnecessary libs

      LIBS=$OLD_LIBS
      CPPFLAGS=$OLD_CPPFLAGS
      LDFLAGS=$OLD_LDFLAGS
    else
      AC_DEFINE(HAVE_ODBC)

      cat <<EOF

ODBC support enabled.

NOTE:
On most systems you need to configure the files
odbc.ini and odbcinst.ini to reflect your installation
(check the manual for your ODBC library).
You may also need to set LD_LIBRARY_PATH (or equivalent)
so that libodbc.so finds the appropriate files.

EOF

    fi
  fi

  if test "x$pike_cv_odbc" = "xyes"; then

    PIKE_FEATURE_OK(Odbc)

    define(PIKE_CHECK_ODBC_TYPEDEF, [
      AC_MSG_CHECKING([if the typedef $1 exists])
      AC_CACHE_VAL(pike_cv_have_$1, [
        AC_TRY_COMPILE([
#ifdef HAVE_ISQL_H

#include <isql.h>

#else /* !HAVE_ISQL_H */

#ifdef HAVE_WINDOWS_H
#include <windows.h>
#else /* !HAVE_WINDOWS_H */
#ifdef HAVE_QEODBC_H
#include <qeodbc.h>
#endif /* HAVE_QEODBC_H */
#endif /* HAVE_WINDOWS_H */

#ifdef HAVE_SQL_H
#include <sql.h>
#endif /* HAVE_SQL_H */

#endif /* HAVE_ISQL_H */

#ifdef HAVE_ISQLEXT_H
#include <isqlext.h>

#else /* !HAVE_ISQLEXT_H */

#ifdef HAVE_SQLEXT_H
#include <sqlext.h>
#endif /* HAVE_SQLEXT_H */

#endif /* HAVE_ISQLEXT_H */
        ], [
          $1 len = 17;
        ], [ pike_cv_have_$1=yes ], [ pike_cv_have_$1=no ])
      ])
      if test "x$pike_cv_have_$1" = "xyes"; then
        AC_MSG_RESULT(yes)
        AC_DEFINE(HAVE_$1)
      else
        AC_MSG_RESULT(no)
      fi
    ])
    PIKE_CHECK_ODBC_TYPEDEF(SQLSMALLINT)
    PIKE_CHECK_ODBC_TYPEDEF(SQLUSMALLINT)
    PIKE_CHECK_ODBC_TYPEDEF(SQLINTEGER)
    PIKE_CHECK_ODBC_TYPEDEF(SQLUINTEGER)
    PIKE_CHECK_ODBC_TYPEDEF(SQLLEN)
    PIKE_CHECK_ODBC_TYPEDEF(SQLULEN)
    PIKE_CHECK_ODBC_TYPEDEF(SQLHENV)
    PIKE_CHECK_ODBC_TYPEDEF(SQLHDBC)
    PIKE_CHECK_ODBC_TYPEDEF(SQLHSTMT)
  else :; fi
else
  :
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )
