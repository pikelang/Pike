#
# Configure script for the oracle module
#

AC_INIT(oracle.cmod)
AC_CONFIG_HEADER(config.h)

AC_MODULE_INIT()

ORACLE_LIBS=""

AC_ARG_WITH(oracle,  [  --without-oracle       no support for the Oracle databases],[],[with_oracle=yes])

PIKE_FEATURE_WITHOUT(Oracle)

if test x$with_oracle = xyes; then

  PIKE_FEATURE_NODEP(Oracle)

  PIKE_SELECT_ABI

  AC_MSG_CHECKING(if this is HP-UX)
  if test "x$pike_cv_sys_os" = "xHP-UX"; then
    AC_MSG_RESULT(yes - disabling dynamic loading for this module)
    dynamic_module_makefile="$static_module_makefile"
  else
    AC_MSG_RESULT(no)
  fi

  AC_MSG_CHECKING(if \$ORACLE_HOME is set)

  AC_CACHE_VAL(pike_cv_oracle_oracle_home, [
    if test x"$ORACLE_HOME" = x; then
      pike_cv_oracle_oracle_home=no
    else
      pike_cv_oracle_oracle_home="`echo \"$ORACLE_HOME\" | sed -e 's://*$::'`"
    fi
  ])

  AC_MSG_RESULT($pike_cv_oracle_oracle_home)

  if test x"$pike_cv_oracle_oracle_home" = xno; then :; else
    AC_DEFINE_UNQUOTED(ORACLE_HOME, "${pike_cv_oracle_oracle_home}")
  fi

  AC_MSG_CHECKING(if \$ORACLE_SID is set)

  AC_CACHE_VAL(pike_cv_oracle_oracle_sid, [
    if test x"$ORACLE_SID" = x; then
      pike_cv_oracle_oracle_sid=no
    else
      pike_cv_oracle_oracle_sid="$ORACLE_SID"
    fi
  ])

  AC_MSG_RESULT($pike_cv_oracle_oracle_sid)

  if test x"$pike_cv_oracle_oracle_sid" = xno; then :; else
    AC_DEFINE_UNQUOTED(ORACLE_SID, "${pike_cv_oracle_oracle_sid}")
  fi

  AC_MSG_CHECKING(for Oracle oratab)

  oratab_locations="$with_root/var/opt/oracle/oratab $with_root/etc/oratab"

  AC_CACHE_VAL(pike_cv_oracle_oratab_file, [

    for pike_cv_oracle_oratab_file in $oratab_locations no; do
      if test -f $pike_cv_oracle_oratab_file; then
        break
      else
        :
      fi
    done

  ])

  AC_MSG_RESULT($pike_cv_oracle_oratab_file)

  AC_MSG_CHECKING(for Gentoo-style Oracle environment file)

  AC_CACHE_VAL(pike_cv_oracle_oraenv_file, [
    for pike_cv_oracle_oraenv_file in "$with_root/etc/env.d/"*oracle* no; do
      if test -f "$pike_cv_oracle_oraenv_file"; then
        if $EGREP '^ORACLE_HOME' "$pike_cv_oracle_oraenv_file" >/dev/null; then
	  break
	else
	  :
	fi
      else
	:
      fi
    done
  ])

  AC_MSG_RESULT($pike_cv_oracle_oraenv_file)

  AC_MSG_CHECKING(for Oracle libraries)

  echo no > conftest

  if test "x$pike_cv_sys_os" = "xWindows_NT"; then
    if test x"$pike_cv_oracle_oracle_home" = xno; then :; else
      echo "$pike_cv_oracle_oracle_home/oci/lib/msvc" > conftest
    fi
  else
    if test x"$pike_cv_oracle_oracle_home" != xno && \
  	test -f "$pike_cv_oracle_oracle_home/lib/libcommon.a" -o \
  	     -f "$pike_cv_oracle_oracle_home/lib/libcommon8.a" -o \
  	     -f "$pike_cv_oracle_oracle_home/lib/libcommon9.a" -o \
  	     -f "$pike_cv_oracle_oracle_home/lib/libcommon10.a" -o \
	     -f "$pike_cv_oracle_oracle_home/lib/libcommon11.a" -o \
	     -f "$pike_cv_oracle_oracle_home/lib/libcommon12.a" -o \
	     -f "$pike_cv_oracle_oracle_home/lib/libclntsh.so" ; then

      echo "$pike_cv_oracle_oracle_home/lib" > conftest

    else
      if test x"$pike_cv_oracle_oratab_file" = xno; then
	if test x"$pike_cv_oracle_oraenv_file" = xno; then :; else
	  sed -e 's/^ORACLE_HOME=\(.*\)/\1\/lib/p' -ed < "$pike_cv_oracle_oraenv_file" > conftest
	fi
      else

        sed -e '/^#/d' < $pike_cv_oracle_oratab_file | while IFS=":" read sid dir bootstart; do
          if test -d "$dir/." && \
	     test -f "$dir/lib/libcommon.a" -o \
		  -f "$dir/lib/libcommon8.a" -o \
		  -f "$dir/lib/libcommon9.a" -o \
		  -f "$dir/lib/libcommon10.a" -o \
		  -f "$dir/lib/libcommon11.a" -o \
		  -f "$dir/lib/libcommon12.a" -o \
	          -f "$dir/lib/libclntsh.so"; then
  	    if test x"$pike_cv_oracle_oracle_home" = xno; then
  	      AC_DEFINE_UNQUOTED(ORACLE_HOME, "${dir}")
  	    else
  	      :
  	    fi
  	    if test x"$pike_cv_oracle_oracle_sid" = xno; then
  	      AC_DEFINE_UNQUOTED(ORACLE_SID, "${sid}")
  	    else
  	      :
  	    fi
  	    echo "$dir/lib" > conftest
  	    break
  	  else
  	    :
  	  fi
  	done
      fi
    fi
  fi

  oracle_lib_dir="`cat conftest`"
  AC_MSG_RESULT($oracle_lib_dir)

  if test "x$oracle_lib_dir" = "xno"; then :; else
    for suff in $pike_cv_abi_suffixes; do
      d="$oracle_lib_dir$suff"
      PIKE_CHECK_ABI_DIR($d, [
        oracle_lib_dir="$d"
        break
      ])
    done

    AC_MSG_CHECKING(abi adjusted oracle_lib_dir)
    AC_MSG_RESULT($oracle_lib_dir)
  fi

  AC_MSG_CHECKING(for Oracle includes)

  echo no > conftest

  if test "x$pike_cv_sys_os" = "xWindows_NT"; then
    if test x"$pike_cv_oracle_oracle_home" = xno; then :; else
      echo "$pike_cv_oracle_oracle_home" > conftest
    fi
  else
    if test x"$pike_cv_oracle_oracle_home" != xno -a \
      -f "$pike_cv_oracle_oracle_home/rdbms/demo/ocidfn.h"; then

      echo "$pike_cv_oracle_oracle_home" > conftest

    elif test x"$pike_cv_oracle_oracle_home" != xno -a \
      -f "$pike_cv_oracle_oracle_home/rdbms/public/ocidfn.h"; then

      echo "$pike_cv_oracle_oracle_home" > conftest

    else
      if test x"$pike_cv_oracle_oratab_file" = xno; then
	if test x"$pike_cv_oracle_oraenv_file" = xno; then :; else
	  sed -e 's/^ORACLE_HOME=//p' -ed < "$pike_cv_oracle_oraenv_file" > conftest
	fi
      else

  	sed -e '/^#/d' < $pike_cv_oracle_oratab_file | while IFS=":" read sid dir bootstart; do
  	  if test -d "$dir/." -a -f "$dir/rdbms/demo/ocidfn.h"; then
  	    echo "$dir" > conftest
  	    break
  	  elif test -d "$dir/." -a -f "$dir/rdbms/public/ocidfn.h"; then
  	    echo "$dir" > conftest
  	    break
  	  else
  	    :
  	  fi
  	done
      fi
    fi
  fi

  oracle_include_dir="`cat conftest`"

  if test "$oracle_include_dir" = "no"; then
    AC_MSG_RESULT(no)
  elif test -f "$oracle_include_dir/ocidfn.h"; then
    AC_MSG_RESULT($oracle_include_dir/)
  elif test -f "$oracle_include_dir/rdbms/demo/ocidfn.h"; then
    AC_MSG_RESULT($oracle_include_dir/rdbms/demo/)
  else
    AC_MSG_RESULT($oracle_include_dir/rdbms/public/)
  fi

  AC_MSG_CHECKING(for sysliblist)

  AC_CACHE_VAL(pike_cv_oracle_sysliblist, [

    echo no > conftest

    if test x"$pike_cv_oracle_oracle_home" != xno -a \
        -f "$pike_cv_oracle_oracle_home/rdbms/lib/sysliblist"; then

      echo "$pike_cv_oracle_oracle_home/rdbms/lib/sysliblist" > conftest

    elif test x"$pike_cv_oracle_oracle_home" != xno -a \
        -f "$pike_cv_oracle_oracle_home/lib/sysliblist"; then

      echo "$pike_cv_oracle_oracle_home/lib/sysliblist" > conftest

    else
      if test x"$pike_cv_oracle_oratab_file" = xno; then :; else

        sed -e '/^#/d' < $pike_cv_oracle_oratab_file | while IFS=":" read sid dir bootstart; do
          if test -d "$dir/." -a -f "$dir/rdbms/lib/sysliblist"; then
  	    echo "$dir/rdbms/lib/sysliblist" > conftest
	    break
          elif test -d "$dir/." -a -f "$dir/lib/sysliblist"; then
  	    echo "$dir/lib/sysliblist" > conftest
	    break
          else
	    :
          fi
        done
      fi
    fi

    oracle_sysliblist="`cat conftest`"

    if test x"$oracle_sysliblist" = xno; then
      pike_cv_oracle_sysliblist=""
    else
      pike_cv_oracle_sysliblist="`cat "$oracle_sysliblist"`"
    fi
  ])

  AC_MSG_RESULT($pike_cv_oracle_sysliblist)

  rm -f conftest
  AC_CHECK_LIB(aio, aioread, [oracle_aio=-laio], [oracle_aio=""])

  if test "x$oracle_lib_dir" = xno -o "x$oracle_include_dir" = xno; then
    if test "x$enable_binary" = "xno"; then
      AC_DEFINE(HAVE_ORACLE)
      PIKE_FEATURE_OK(Oracle)
    fi
  else

    CPPFLAGS="-I$oracle_include_dir -I$oracle_include_dir/oci/include -I$oracle_include_dir/rdbms/demo/ -I$oracle_include_dir/rdbms/public/ -I$oracle_include_dir/network/public/ -I$oracle_include_dir/plsql/public -I$oracle_include_dir/otrace/public ${CPPFLAGS}"
    LDFLAGS="${LDFLAGS} -L$oracle_lib_dir -R$oracle_lib_dir"

    if test "x$pike_cv_sys_os" = "xWindows_NT"; then
      ORACLE_LIBS="-loci"
    else
      if test -f "$oracle_lib_dir/libcommon8.a" -o \
              -f "$oracle_lib_dir/libcommon9.a" -o \
              -f "$oracle_lib_dir/libcommon10.a" -o \
              -f "$oracle_lib_dir/libcommon11.a" -o \
	      -f "$oracle_lib_dir/libclntsh.so"; then
        ORACLE_LIBS="-lclntsh"
      else
        if test -f "$oracle_lib_dir/libcore4.a"; then
          ORACLE_LIBS="-lclntsh -lcommon -lcore4 -lnlsrtl3 $pike_cv_oracle_sysliblist $oracle_aio -lm -lcore4"
        else
          ORACLE_LIBS="-lclient -lsqlnet -lncr -lsqlnet -lclient -lcommon -lgeneric -lsqlnet -lncr -lsqlnet -lclient -lcommon -lgeneric -lepc -lnlsrtl3 -lc3v6 -lcore3 -lnlsrtl3 -lcore3 -lnlsrtl3 $pike_cv_oracle_sysliblist $oracle_aio -lm -lcore3"
	fi
      fi
    fi

    AC_CHECK_HEADERS(oci.h ocidfn.h ociapr.h)

    OLDLIBS=LIBS
    LIBS="$ORACLE_LIBS $LIBS"

    AC_MSG_CHECKING(for working Oracle libs)
    AC_CACHE_VAL(pike_cv_working_oracle,[
      AC_TRY_LINK([
#ifdef HAVE_OCI_H
#include <oci.h>
#else /* !HAVE_OCI_H */
#include <ocidfn.h>
#include <ociapr.h>
#endif /* HAVE_OCI_H */
],[
  OCIInitialize(OCI_DEFAULT,0,0,0,0);
],pike_cv_working_oracle=yes,pike_cv_working_oracle=no)
    ])
    AC_MSG_RESULT($pike_cv_working_oracle)
    if test x$pike_cv_working_oracle = xyes ; then
      AC_DEFINE(HAVE_ORACLE)
      LDFLAGS="$EXTRALDFLAGS $LDFLAGS"
      PIKE_FEATURE_OK(Oracle)
    else
      ORACLE_LIBS=""
      LIBS="$OLDLIBS"
      PIKE_FEATURE(Oracle,[no (does not work)])
    fi
  fi

  EXTRALDFLAGS=""

  AC_MSG_CHECKING(if ld supports -Bsymbolic)
  AC_CACHE_VAL(pike_cv_oracle_ld_bsymbolic, [
    pike_cv_oracle_ld_bsymbolic=no
    cat > conftest.$ac_ext <<EOF
int foo(){ return 0; }
EOF
    if AC_TRY_EVAL(ac_compile); then
      if AC_TRY_COMMAND([ld -r -o conftest2.o conftest.o $EXTRALDFLAGS -Bsymbolic $LIBS]); then
        if test -f conftest2.o; then
          pike_cv_oracle_ld_bsymbolic=yes
	else
	  :
	fi
      else
        :
      fi
    else
      echo "configure: failed program was:" >&AC_FD_CC
      cat conftest.$ac_ext >&AC_FD_CC
    fi
    rm -f conftest*
  ])
  AC_MSG_RESULT($pike_cv_oracle_ld_bsymbolic)

  if test "x$pike_cv_oracle_ld_bsymbolic" = xyes; then
    EXTRALDFLAGS="$EXTRALDFLAGS -Bsymbolic"
  else
    :
  fi

  AC_MSG_CHECKING(if ld supports -znodefs)
  AC_CACHE_VAL(pike_cv_oracle_ld_znodefs, [
    pike_cv_oracle_ld_znodefs=no
    cat > conftest.$ac_ext <<EOF
int foo(){ return 0; }
EOF
    if AC_TRY_EVAL(ac_compile); then

      dnl  Some ld:s interpret -znodefs as "-z -n -o defs".
      dnl  Check for such ld:s.

      if AC_TRY_COMMAND([ld -r -o conftest2.o conftest.o $EXTRALDFLAGS -zoconftest3.o $LIBS]) && \
	  test -f conftest3.o; then
	:
      else
        if AC_TRY_COMMAND([ld -r -o conftest2.o conftest.o $EXTRALDFLAGS -znodefs $LIBS]); then
          if test -f conftest2.o; then
            pike_cv_oracle_ld_znodefs=yes
	  else
	    :
	  fi
        else
          :
        fi
      fi
    else
      echo "configure: failed program was:" >&AC_FD_CC
      cat conftest.$ac_ext >&AC_FD_CC
    fi
    rm -f conftest*
  ])
  AC_MSG_RESULT($pike_cv_oracle_ld_znodefs)

  if test "x$pike_cv_oracle_ld_bsymbolic$pike_cv_oracle_ld_znodefs" = xyesyes; then
    EXTRALDFLAGS="$EXTRALDFLAGS -znodefs"
  else
    :
  fi

else
  :
fi

AC_SUBST(ORACLE_LIBS)

AC_OUTPUT(Makefile,echo FOO >stamp-h )
