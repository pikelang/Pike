# $Id: configure.in,v 1.12 1999/11/27 00:33:02 grubba Exp $
AC_INIT(image.c)
AC_CONFIG_HEADER(image_machine.h)

AC_ARG_WITH(gif-rle,      [  --with-gif-rle        use kind-of-rle packing instead of lzw],[AC_DEFINE(GIF_LZW_RLE)],[])

AC_SET_MAKE

AC_MODULE_INIT()

AC_FUNC_MMAP

AC_CHECK_HEADERS(math.h sys/fcntl.h fcntl.h stdlib.h netinet/in.h winsock.h)

AC_CHECK_LIB(m, abs, [], [])

# Perform this test by hand since Sun cc fails otherwise.
AC_MSG_CHECKING([for fabs in -lm])
AC_CACHE_VAL(pike_cv_lib_m_fabs,
[
  OLDLIBS="$LIBS"
  LIBS="-lm $LIBS"
  AC_TRY_LINK(
  [
#ifdef HAVE_MATH_H
#include <math.h>
#else /* !HAVE_MATH_H */
    double fabs(double);
#endif /* HAVE_MATH_H */
  ],[
    double foo = fabs(1.0);
  ],[pike_cv_lib_m_fabs=yes],[pike_cv_lib_m_fabs=no])
  LIBS="$OLDLIBS"
])
if test "x$pike_cv_lib_m_fabs" = "xyes"; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_LIBM)
  LIBS="-lm $LIBS"
else
  AC_MSG_RESULT(no)
fi

AC_SUBST(RANLIB)

AC_CONFIG_SUBDIRS(encodings)

AC_OUTPUT(Makefile,echo FOO >stamp-h )


