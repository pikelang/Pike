# $Id: configure.in,v 1.28 2004/10/21 15:52:54 grubba Exp $
AC_INIT(image.c)
AC_CONFIG_HEADER(image_machine.h)

AC_SET_MAKE

AC_MODULE_INIT()

PIKE_FEATURE_OK(Image)

AC_FUNC_MMAP

AC_CHECK_HEADERS(math.h sys/fcntl.h fcntl.h stdlib.h netinet/in.h winsock.h winsock2.h)

AC_CHECK_LIB(m, abs, [], [])

# Do we have nasm?
AC_SUBST(ASSEMBLY_OBJECTS)
ASSEMBLY_OBJECTS=""
AC_MSG_CHECKING( [if nasm can be used] )

if test "x$with_machine_code" = "xno" ; then
  AC_MSG_RESULT(no)
else
  if nasm -f elf -o assembly.o $srcdir/assembly.s ; then
    # nasm/ia64 can compile ia32 code...
    if test "`uname -m`" = "ia64"; then
      AC_MSG_RESULT([yes, but we are on an IA64 machine])
    elif test "`uname -m`" = "x86_64"; then
      AC_MSG_RESULT([yes, but we are on an x86_64 machine])
    elif test "`uname -s`" = "OpenBSD"; then
      #nasm on OpenBSD is braindamaged
      AC_MSG_RESULT(no)
    elif test x"${with_valgrind:-no}" != xno; then
      AC_MSG_RESULT([no - valgrind is used])
    else
      ASSEMBLY_OBJECTS=assembly.o
      AC_DEFINE(ASSEMBLY_OK)
      AC_MSG_RESULT(yes)
    fi
  else
    AC_MSG_RESULT(no)
  fi
fi

# Perform this test by hand since Sun cc fails otherwise.
AC_MSG_CHECKING([for fabs in -lm])
AC_CACHE_VAL(pike_cv_lib_m_fabs,
[
  OLDLIBS="$LIBS"
  LIBS="-lm $LIBS"
  AC_TRY_LINK(
  [
#ifdef HAVE_MATH_H
#include <math.h>
#else /* !HAVE_MATH_H */
    double fabs(double);
#endif /* HAVE_MATH_H */
  ],[
    double foo = fabs(1.0);
  ],[pike_cv_lib_m_fabs=yes],[pike_cv_lib_m_fabs=no])
  LIBS="$OLDLIBS"
])
if test "x$pike_cv_lib_m_fabs" = "xyes"; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_LIBM)
  LIBS="-lm $LIBS"
else
  AC_MSG_RESULT(no)
fi

if test x"$pike_cv_sys_os" = xWindows_NT ; then
  # FIXME: These look suspiciouly much like 32bit versions.
  #        We probably want to use 64bit versions on IA64.
  LDFLAGS="-lws2_32  $LDFLAGS"
fi


AC_SUBST(RANLIB)

AC_CONFIG_SUBDIRS(encodings)

AC_OUTPUT(Makefile,echo FOO >stamp-h )
