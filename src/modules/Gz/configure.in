# $Id$
AC_INIT(zlibmod.c)
AC_CONFIG_HEADER(zlib_machine.h)
AC_ARG_WITH(zlib,     [  --with(out)-zlib       Support gzip compression],[],[with_zlib=yes])

AC_MODULE_INIT()

PIKE_FEATURE_WITHOUT(Gz)

define([AC_CHECK_GZ],
[
  done=no
  AC_MSG_CHECKING(working lib$1)
  AC_CACHE_VAL(pike_cv_working_$1,[
    OLDLIBS="${LIBS-}"
    LIBS="${LIBS-} -l$1"
    AC_TRY_RUN([
`cat $srcdir/gz_test.c`
    ],pike_cv_working_$1=yes,pike_cv_working_$1=no,
      AC_TRY_LINK([
`cat $srcdir/gz_test.c`
#define main notmain
      ],[],pike_cv_working_$1=yes,pike_cv_working_$1=no)
    )
  ])
  LIBS="$OLDLIBS"

  if test "x$pike_cv_working_$1" = xyes; then
    PIKE_FEATURE(Gz,[yes (lib$1)])
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_LIBZ)
    LIBS="${LIBS-} -l$1"
    done=yes
  else
    AC_MSG_RESULT(no)
  fi

  if test $done = no; then
    $2
    :
  fi
])

if test x$with_zlib = xyes ; then
  PIKE_FEATURE_NODEP(Gz)
  AC_CHECK_HEADERS(zlib.h)
  if test $ac_cv_header_zlib_h = yes ; then
    AC_MSG_CHECKING([if zlib.h is new enough])
    AC_TRY_LINK([
#include <zlib.h>
    ],[
int foo = (int)(Z_NO_COMPRESSION | Z_VERSION_ERROR);
    ],[ AC_MSG_RESULT(yes) ],[ AC_MSG_RESULT(no); ac_cv_header_zlib_h=no ])

    PIKE_FEATURE(Gz,[no (zlib is too old)])
    if test $ac_cv_header_zlib_h = yes ; then
      if test x$pike_cv_sys_os = xIRIX ; then
        # The libz.so supplied with IRIX 6.3 needs these obscure symbols
        # C++? It also differs which library which contains them.
        AC_CHECK_LIB(Csup, __vtbl__9type_info)
	if test "$ac_cv_lib_Csup___vtbl__9type_info" = "yes" ; then :; else
          AC_CHECK_LIB(C, __vtbl__9type_info)
	fi
	AC_CHECK_LIB(Csup, __T_9__nothrow)
	AC_HAVE_FUNCS(__vtbl__9type_info)
	AC_HAVE_FUNCS(__T_9__nothrow)
      fi

      AC_CHECK_GZ(z,[AC_CHECK_GZ(gz,[ ac_cv_lib_z_main=no ] ) ])

    fi
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )


