# $Id$
AC_INIT(top.c)
AC_CONFIG_HEADER(config.h)

AC_MODULE_INIT()

AC_ARG_WITH(GL,  [  --without-GL       no support for GL],[],[with_GL=yes])

AUTO_OBJS=""

if test x$with_GL = xno; then
  :
else

  AC_PATH_XTRA()

  AC_ARG_WITH(lib-GL,     [  --with-lib-GL           use '-lGL'])
  AC_ARG_WITH(lib-MesaGL, [  --with-lib-MesaGL       use '-lMesaGL'])

  old_CPPFLAGS="$CPPFLAGS"
  CPPFLAGS="$X_CFLAGS $CPPFLAGS"
  AC_CHECK_HEADERS(windef.h wingdi.h GL/gl.h GL/glx.h)
  CPPFLAGS="$old_CPPFLAGS"

  if test "x$ac_cv_header_windef_h$ac_cv_header_wingdi_h$ac_cv_header_GL_gl_h$ac_cv_header_GL_glx_h" = "xnononono"; then
    # Solaris 7 comes with libGL, but without the headerfiles by default.
    :
  else

    AC_CHECK_LIB(X11,XDrawPoint)
    AC_CHECK_LIB(Xext,XShmPutImage)

    AC_CHECK_LIB(GL, glOrtho, AC_DEFINE_UNQUOTED(HAVE_LIBGL),,[$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS])
    AC_CHECK_LIB(MesaGL, glOrtho, AC_DEFINE_UNQUOTED(HAVE_LIBMESAGL),,[$X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS])
    oldLIBS="$LIBS"
    AC_MSG_CHECKING(for glOrtho in -lOpenGL)
    AC_CACHE_VAL(pike_cv_lib_OpenGL, [
      LIBS="$oldLIBS -lOpenGL"
      AC_TRY_LINK([
#ifdef HAVE_WINDEF_H
#include <windef.h>
#endif /* HAVE_WINDEF_H */
#ifdef HAVE_WINGDI_H
#include <wingdi.h>
#endif /* HAVE_WINGDI_H */
#ifdef HAVE_GL_GL_H
#include <GL/gl.h>
#endif /* HAVE_GL_GL_H */
      ], [ glOrtho(0.0,0.0,0.0,0.0,0.0,0.0); ], [ pike_cv_lib_OpenGL=yes ], [ pike_cv_lib_OpenGL=no ])
    ])
    if test x"$pike_cv_lib_OpenGL" = xyes; then
      AC_MSG_RESULT(yes)
      AC_DEFINE_UNQUOTED(HAVE_LIBOPENGL)
    else
      AC_MSG_RESULT(no)
    fi
    AC_MSG_CHECKING(for glOrtho in -lOpenGL32)
    AC_CACHE_VAL(pike_cv_lib_OpenGL32, [
      LIBS="$oldLIBS -lOpenGL32"
      AC_TRY_LINK([
#include <windows.h>
#ifdef HAVE_WINDEF_H
#include <windef.h>
#endif /* HAVE_WINDEF_H */
#ifdef HAVE_WINGDI_H
#include <wingdi.h>
#endif /* HAVE_WINGDI_H */
#ifdef HAVE_GL_GL_H
#include <GL/gl.h>
#endif /* HAVE_GL_GL_H */
      ], [ glOrtho(0.0,0.0,0.0,0.0,0.0,0.0); ], [ pike_cv_lib_OpenGL32=yes ], [ pike_cv_lib_OpenGL32=no ])
    ])
    if test x"$pike_cv_lib_OpenGL32" = xyes; then
      AC_MSG_RESULT(yes)
      AC_DEFINE_UNQUOTED(HAVE_LIBOPENGL32)
    else
      AC_MSG_RESULT(no)
    fi
    LIBS="$oldLIBS"

    tmp_gl_fail=no
    if test x"$with_lib_GL" = "xyes" -a x"$ac_cv_lib_GL_glOrtho" = xyes; then
      LIBS="-lGL $LIBS $X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS"
      AC_DEFINE(HAVE_GL)
    elif test x"$with_lib_GL" = "xyes" -a x"$pike_cv_lib_OpenGL" = xyes; then
      LIBS="-lOpenGL $LIBS"
      AC_DEFINE(HAVE_GL)
    elif test x"$with_lib_GL" = "xyes" -a x"$pike_cv_lib_OpenGL32" = xyes; then
      LIBS="-lOpenGL32 $LIBS"
      AC_DEFINE(HAVE_GL)
    elif test x"$with_lib_MesaGL" = "xyes" -a x"$ac_cv_lib_MesaGL_glOrtho" = xyes; then
      LIBS="-lMesaGL $LIBS $X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS"
    elif test x"$ac_cv_lib_GL_glOrtho" = xyes; then
      LIBS="-lGL $LIBS"
    elif test x"$pike_cv_lib_OpenGL" = xyes; then
      LIBS="-lOpenGL $LIBS"
    elif test x"$pike_cv_lib_OpenGL32" = xyes; then
      LIBS="-lOpenGL32 $LIBS"
    elif test x"$ac_cv_lib_MesaGL_glOrtho" = xyes; then
      LIBS="-lMesaGL $LIBS $X_LIBS $X_PRE_LIBS -lXext -lX11 $X_EXTRA_LIBS"
    else
      AC_MSG_WARN([No GL or MesaGL libraries, disabling GL support.])
      tmp_gl_fail=yes
    fi
    
    if test x"$tmp_gl_fail" = xno; then
      AC_DEFINE(HAVE_GL)
      AUTO_OBJS="auto.o"
    fi
    
  fi

fi

AC_SUBST(AUTO_OBJS)
AC_OUTPUT(Makefile,echo FOO >stamp-h )
