# $Id$
AC_INIT(libbzip2mod.cmod)
AC_CONFIG_HEADER(libbzip2mod_config.h)
AC_ARG_WITH(bz2,     [  --without-bz2       Disable Bz2],[],[with_bz2=yes])

AC_MODULE_INIT()

PIKE_FEATURE_WITHOUT(Bz2)

if test x$with_bz2 = xyes ; then
  PIKE_FEATURE(Bz2,[no (missing libbz2)])

  # NB: bzCompressInit always exists in bz2.
  # no, it doesn't. /Mirar

  AC_CHECK_LIB(bz2, bzCompressInit)
  AC_CHECK_LIB(bz2, BZ2_bzCompressInit)

  if test "x$ac_cv_lib_bz2_BZ2_bzCompressInit" = xyes \
       -o "x$ac_cv_lib_bz2_bzCompressInit" = xyes; then

    # if BZ2_bzCompressInit exists,
    # it should be used instead of bzCompressInit.
    AC_CHECK_FUNCS(BZ2_bzCompressInit)

    AC_CHECK_HEADERS(bzlib.h string.h strings.h)
    if test $ac_cv_header_bzlib_h = yes ; then
      PIKE_FEATURE(Bz2,[yes (using libbz2)])
      AC_DEFINE(HAVE_BZ2LIB)

      AC_MSG_CHECKING([if a bz_stream has the field total_out.])
      AC_CACHE_VAL([pike_cv_bz_stream_total_out],[
	AC_TRY_COMPILE([
/* bzlib.h needs stdio.h on some systems (for FILE) */
#include <stdio.h>
#include <bzlib.h>
	],[
	  extern bz_stream *s;
	  s->total_out = 0;
	],[ pike_cv_bz_stream_total_out="yes"
	],[ pike_cv_bz_stream_total_out="no" ])
      ])
      AC_MSG_RESULT($pike_cv_bz_stream_total_out)
      if test "x$pike_cv_bz_stream_total_out" = xyes; then
	AC_DEFINE(HAVE_BZ_STREAM_TOTAL_OUT)
      fi
    else
       PIKE_FEATURE(Bz2,[no (got libbz2 but missing bzlib.h)])
    fi
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )


