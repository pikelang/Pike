AC_INIT(svg.c)

AC_ARG_WITH(svg,[  --with(out)-svg       Support SVG via librsvg (from gnome2) (Image.SVG)],[],[with_svg=yes])

AC_MODULE_INIT()
AC_CONFIG_HEADER(config.h)

PIKE_FEATURE_WITHOUT(Image.SVG)

AC_SUBST(SVG_CFLAGS)
AC_SUBST(SVG_LIBS)

if test x$with_svg = xyes ; then
  PIKE_FEATURE_NODEP(Image.SVG)

  PIKE_PROG_PKG_CONFIG

  if ${PKG_CONFIG} librsvg-2.0 2>/dev/null ; then
    SVG_CFLAGS="`${PKG_CONFIG} librsvg-2.0 --cflags`"
    SVG_LIBS="`${PKG_CONFIG} librsvg-2.0 --libs`"
    SVG_VERSION="`${PKG_CONFIG} librsvg-2.0 --modversion`"
    AC_MSG_CHECKING(if librsvg actually works)

    OLDCPPFLAGS="$CPPFLAGS"
    OLDLIBS="$LIBS"
    CPPFLAGS="$SVG_CFLAGS $CPPFLAGS"
    LIBS="$SVG_LIBS $LIBS"

    AC_TRY_LINK([
#include <librsvg/rsvg.h>
],
[
  RsvgHandle *handle;
  handle = rsvg_handle_new();
],
[svg_ok=yes],
[svg_ok=no])
    if test yes = "$svg_ok" ; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(HAVE_SVG)
      if test `echo "$SVG_VERSION"|cut -d. -f1` -gt 2 -o `echo "$SVG_VERSION"|cut -d. -f2` -ge 36 ; then
        AC_DEFINE(HAVE_SVG_2_36_OR_NEWER, 1, [Define if librsvg is version 2.36 or newer])
      fi
      PIKE_FEATURE_OK(Image.SVG)

      AC_CHECK_FUNCS(rsvg_handle_get_intrinsic_size_in_pixels \
		     rsvg_handle_render_document)
    else
      AC_MSG_RESULT(no)
      SVG_CFLAGS=""
      SVG_LIBS=""
      SVG_VERSION=""
      CPPFLAGS="$OLDCPPFLAGS"
      LIBS="$OLDLIBS"
    fi
  fi
fi

AC_OUTPUT(Makefile,echo FOO >stamp-h )
